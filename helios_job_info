#!/usr/bin/env perl

use 5.008;
use strict;
use warnings;
use Getopt::Long;

use Helios::Job;

our $VERSION = '2.90_0000';

our $OPT_JOBID = '';
our $OPT_ARGS  = 0;
our $OPT_LOGS  = 0;
our $OPT_HELP  = 0;
our @LOG_PRIORITIES = qw(EMERG ALERT CRIT ERR WARNING NOTICE INFO DEBUG);

# 1st arg is jobid if it doesn't start with -
if ($ARGV[0] !~ /^-/) {
	$OPT_JOBID = shift @ARGV;
}

GetOptions(
	"jobid=s" => \$OPT_JOBID,
	"args"    => \$OPT_ARGS,
	"logs"    => \$OPT_LOGS,
	"help"    => \$OPT_HELP,
);

# help mode
if ($OPT_HELP) {
	require Pod::Usage;
	Pod::Usage::pod2usage(-verbose => 2, -exitstatus => 0);
}

unless ($OPT_JOBID) {
	warn "$0: A jobid is required.\n";
	exit(1);
}

my $job;

eval {
	$job = Helios::Job->lookup(jobid => $OPT_JOBID);

	1;
} or do {
	my $E = $@;
	warn "$0: ERROR: $E\n";
	exit(1);	
};

if ( defined $job )  {
	print "Jobid: ", $job->jobid,"\n";
	print "Jobtype: ", $job->getJobType(),"\n";
	print "Submit Time: ", scalar localtime($job->getInsertTime()),"\n";
	print "Run After: ", scalar localtime($job->getRunAfter()),"\n";
	print "Locked Until: ";
	print scalar localtime($job->getLockedUntil()) if $job->getLockedUntil();
	print "\n";
	print "Priority: ";
	print $job->getPriority if defined $job->getPriority;
	print "\n";
	print "Complete Time: ";
	print scalar localtime($job->getCompleteTime()) if defined $job->getCompleteTime();
	print "\n";
	print "Exitstatus: ";
	print $job->getExitstatus() if defined $job->getExitstatus;
	print "\n";
	print "\n";
	if ($OPT_ARGS) {
		print "Args: \n";
		print $job->getArgString(),"\n\n";
	}
	
} else {
	print "JOBID: $OPT_JOBID not found.\n";
	exit(1);	
}


my @logs;
if ($OPT_LOGS) {
	eval {
		@logs = $job->get_logs();
		1;
	} or do {
		my $E = $@;
		warn "$0: ERROR: $E\n";
		exit(1);			
	};

	if ( @logs ) {
		print "Logs:\n";
		foreach (@logs) {
			print scalar localtime($_->getLogTime()),' [',$_->getHost(),':',$_->getPid(),'] ',
				$LOG_PRIORITIES[$_->getPriority()],' ',$_->getMessage(),"\n";
		}
		print "\n";
	}
	
}

exit(0);


=head1 NAME

helios_job_info - get detailed information about a job in a Helios collective

=head1 SYNOPSIS

 helios_job_info --jobid=<jobid> [--args] [--logs]
   
 # just display job metadata info for job 12345
 helios_job_info --jobid=12345
    
 # display job metadata, job argument string, and any log messages
 helios_job_info --jobid=12345 --args --logs
 
 # first argument is assumed to be jobid, so "--jobid=" is optional
 # you can also use short form options
 helios_job_info 12345 -a -l


=head1 DESCRIPTION

The helios_job_info command can be used to retrieve information about a job in
the Helios collective database.  It will display job metadata such as jobtype, 
submit time, and (if the job has completed) complete time and exitstatus.  The 
command will also retrieve the job argument string if the --args option is 
specified, and any log messages recorded for the job with the --logs option.

=head1 AUTHOR

Andrew Johnson, E<lt>lajandy at cpan dot orgE<gt>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2013-4 by Logical Helion, LLC.

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself, either Perl version 5.8.0 or,
at your option, any later version of Perl 5 you may have available.

=head1 WARRANTY

This software comes with no warranty of any kind.

=cut
